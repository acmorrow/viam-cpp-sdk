# -*- mode: python; -*-

Import(["env"])

env = env.Clone()


def lvru_system_name_gen(target, source, env, for_signature):
    if env.subst("$HOST_OS") == "darwin":
        return "macosx"
    return "$HOST_OS"


env.Command(
    target="${LIBPREFIX}viam_rust_utils${LIBSUFFIX}",
    source=env.WhereIs("curl"),
    action="$SOURCE -s -L https://github.com/viamrobotics/rust-utils/releases/latest/download/${LIBPREFIX}viam_rust_utils-${LVRU_SYSTEM_NAME}_${HOST_ARCH}${LIBSUFFIX} -o $TARGET",
    LVRU_SYSTEM_NAME=lvru_system_name_gen,
)

# Search cwd for the libviam_rust_utils static library we pulled down.
env = env.Clone()
env.AppendUnique(
    LIBPATH=".",
)

if env.subst("$HOST_OS") == "darwin":
    env.AppendUnique(FRAMEWORKS=["CoreFoundation", "Security"])

libviamsdk = env.SharedLibrary(
    target="viamsdk",
    source=[
        "registry/registry.cpp",  # Note: Must be first for bad link order reasons
        "common/client_helper.cpp",
        "common/linear_algebra.cpp",
        "common/pose.cpp",
        "common/proto_type.cpp",
        "common/service_helper.cpp",
        "common/utils.cpp",
        "common/world_state.cpp",
        "components/base.cpp",
        "components/board.cpp",
        "components/camera.cpp",
        "components/component.cpp",
        "components/encoder.cpp",
        "components/generic.cpp",
        "components/motor.cpp",
        "components/movement_sensor.cpp",
        "components/power_sensor.cpp",
        "components/private/base_client.cpp",
        "components/private/base_server.cpp",
        "components/private/board_client.cpp",
        "components/private/board_server.cpp",
        "components/private/camera_client.cpp",
        "components/private/camera_server.cpp",
        "components/private/encoder_client.cpp",
        "components/private/encoder_server.cpp",
        "components/private/generic_client.cpp",
        "components/private/generic_server.cpp",
        "components/private/motor_client.cpp",
        "components/private/motor_server.cpp",
        "components/private/movement_sensor_client.cpp",
        "components/private/movement_sensor_server.cpp",
        "components/private/power_sensor_client.cpp",
        "components/private/power_sensor_server.cpp",
        "components/private/sensor_client.cpp",
        "components/private/sensor_server.cpp",
        "components/private/servo_client.cpp",
        "components/private/servo_server.cpp",
        "components/sensor.cpp",
        "components/servo.cpp",
        "config/resource.cpp",
        "module/handler_map.cpp",
        "module/module.cpp",
        "module/service.cpp",
        "module/signal_manager.cpp",
        "referenceframe/frame.cpp",
        "resource/resource.cpp",
        "resource/resource_api.cpp",
        "resource/resource_manager.cpp",
        "resource/resource_server_base.cpp",
        "resource/stoppable.cpp",
        "robot/client.cpp",
        "robot/service.cpp",
        "rpc/dial.cpp",
        "rpc/server.cpp",
        "services/generic.cpp",
        "services/mlmodel.cpp",
        "services/motion.cpp",
        "services/private/generic_client.cpp",
        "services/private/generic_server.cpp",
        "services/private/mlmodel.cpp",
        "services/private/mlmodel_client.cpp",
        "services/private/mlmodel_server.cpp",
        "services/private/motion_client.cpp",
        "services/private/motion_server.cpp",
        "services/service.cpp",
        "spatialmath/geometry.cpp",
        "spatialmath/orientation_types.cpp",
        "spatialmath/orientation.cpp",
    ],
    api_headers=[
        "common/client_helper.hpp",
        "common/linear_algebra.hpp",
        "common/pose.hpp",
        "common/proto_type.hpp",
        "common/service_helper.hpp",
        "common/utils.hpp",
        "common/world_state.hpp",
        "components/base.hpp",
        "components/board.hpp",
        "components/camera.hpp",
        "components/component.hpp",
        "components/encoder.hpp",
        "components/generic.hpp",
        "components/motor.hpp",
        "components/movement_sensor.hpp",
        "components/power_sensor.hpp",
        "components/sensor.hpp",
        "components/servo.hpp",
        "config/resource.hpp",
        "module/handler_map.hpp",
        "module/module.hpp",
        "module/service.hpp",
        "module/signal_manager.hpp",
        "referenceframe/frame.hpp",
        "registry/registry.hpp",
        "resource/resource.hpp",
        "resource/resource_api.hpp",
        "resource/resource_manager.hpp",
        "resource/resource_server_base.hpp",
        "resource/stoppable.hpp",
        "robot/client.hpp",
        "robot/service.hpp",
        "rpc/dial.hpp",
        "rpc/server.hpp",
        "services/generic.hpp",
        "services/mlmodel.hpp",
        "services/motion.hpp",
        "services/service.hpp",
        "spatialmath/geometry.hpp",
        "spatialmath/orientation_types.hpp",
        "spatialmath/orientation.hpp",
    ],
    LIBS=[
        "absl_cord",
        "absl_cord_internal",
        "absl_cordz_info",
        "absl_hash",
        "absl_log_internal_check_op",
        "absl_log_internal_message",
        "absl_log_internal_nullguard",
        "boost_log-mt",
        "gpr",
        "grpc",
        "grpc++",
        "grpc++_reflection",
        "protobuf",
        "viam_rust_utils",
        "viamapi",
    ],
)

libviamsdk_install = env.Install(
    target="$DEST_DIR/$PREFIX_LIB_DIR",
    source=libviamsdk,
)
env.Alias("install", [libviamsdk_install])

env = env.Clone(HEADER_ROOT=env.Dir("../..").srcnode())
for header in env.File(libviamsdk[0].get_build_env()["api_headers"]):
    header_install = env.Install(
        target=env.subst(
            "$DEST_DIR/$PREFIX_INCLUDE_DIR/${HEADER_ROOT.rel_path(SOURCE.dir)}",
            source=header,
        ),
        source=header,
    )
    env.Alias("install", header_install)
